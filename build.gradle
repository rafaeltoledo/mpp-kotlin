buildscript {

    ext.versions = [
            'agp'          : '4.0.2',
            'coroutines'   : '1.3.9-native-mt-2',
            'kotlin'       : '1.4.10',
            'ktor'         : '1.4.1',
            'serialization': '1.0.0',
    ]

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$versions.agp"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$versions.kotlin"
    }
}

allprojects {

    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    // Fix while Ktor do not update to serialization 1.0
    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                if (details.requested.name.startsWith('kotlinx-serialization-')) {
                    details.useVersion versions.serialization
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
